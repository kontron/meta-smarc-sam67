#@TYPE: Machine
#@NAME: SMARC SA67
#@DESCRIPTION: Machine configuration for the Kontron Smarc SA67 board

require conf/machine/include/j722s-sa67.inc

TI_PREFERRED_BSP ?= "mainline"

KERNEL_DEVICETREE_PREFIX = ""

KERNEL_DEVICETREE += "ti/k3-am67a-kontron-sa67.dtb \
                      ti/k3-am67a-kontron-sa67-ads2.dtbo \
                      ti/k3-am67a-kontron-sa67-ads2-rev-a-fixups.dtbo \
                      ti/k3-am67a-kontron-sa67-lvds0-evervision-vgg644804.dtbo \
                      ti/k3-am67a-kontron-sa67-lvds-auo-p238han01.dtbo"

UBOOT_MACHINE = "kontron_sa67_a53_defconfig"

# Configure extlinux

UBOOT_EXTLINUX = "1"
UBOOT_EXTLINUX_TIMEOUT = "60"
UBOOT_EXTLINUX_LABELS = "module-base display1 display2"
UBOOT_EXTLINUX_DEFAULT_LABEL ?= "module-base"

UBOOT_EXTLINUX_CONSOLE ?= "console=ttyS1,115200n8"
UBOOT_EXTLINUX_ROOT ?= "root=/dev/mmcblk1p2"
UBOOT_EXTLINUX_KERNEL_IMAGE ?= "/Image"
UBOOT_EXTLINUX_FDT ?= "/k3-am67a-kontron-sa67.dtb"
UBOOT_EXTLINUX_KERNEL_ARGS ?= "rootwait rw"

UBOOT_EXTLINUX_MENU_DESCRIPTION:module-base ?= "SA67 Base Module Only"
UBOOT_EXTLINUX_MENU_DESCRIPTION:module-ads2 ?= "SA67 on ADS2 Carrier"
UBOOT_EXTLINUX_MENU_DESCRIPTION:display1 ?= "Eval Carrier 2.1 with LVDS evervision-vgg644804"
UBOOT_EXTLINUX_MENU_DESCRIPTION:display2 ?= "Eval Carrier 2.1 with LVDS AUO-P238HAN01"

DEVICETREE_FILE = "${@(d.getVar('KERNEL_DEVICETREE') or "").replace("ti/","")}"

IMAGE_BOOT_FILES += " \
	${DEVICETREE_FILE} \
	${KERNEL_IMAGETYPE} \
	extlinux.conf;extlinux/extlinux.conf \
	"

WKS_FILE = "sa67.wks"
